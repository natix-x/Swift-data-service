plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.bankdata'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
    intTestImplementation.extendsFrom implementation, testImplementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'mysql:mysql-connector-java:8.0.32'
	implementation 'com.opencsv:opencsv:5.8'
	runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
	testCompileOnly 'org.projectlombok:lombok:1.18.36'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'com.h2database:h2'
	intTestImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
	intTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.testcontainers:mssqlserver:1.20.5'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.5'
	testImplementation 'org.testcontainers:mysql:1.20.5'
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation 'org.testcontainers:mssqlserver:1.20.5'
    intTestImplementation 'org.testcontainers:junit-jupiter:1.20.5'
    intTestImplementation 'org.testcontainers:mysql:1.20.5'
}


tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	shouldRunAfter test
    dependsOn bootJar
	useJUnitPlatform()

	testLogging {
		events "passed"
	}
}
check.dependsOn integrationTest
